@PACKAGE_INIT@

set(@PROJECT_NAME@_FOUND ON)
set_and_check(@PROJECT_NAME@_INCLUDE_DIRS "${PACKAGE_PREFIX_DIR}/include")
set_and_check(@PROJECT_NAME@_LIBRARY_DIRS "${PACKAGE_PREFIX_DIR}/lib")

include(CMakeFindDependencyMacro)
find_dependency(Eigen3)
find_dependency(console_bridge)
find_dependency(tesseract_common)
find_dependency(octomap)

# Find assimp using PkgConfig because cmake config is broken https://github.com/assimp/assimp/issues/2247
find_dependency(assimp)
if (assimp_VERSION VERSION_LESS "5.0.0")
  find_package(PkgConfig REQUIRED)
  pkg_check_modules(ASSIMP REQUIRED assimp)
  find_path(PC_ASSIMP_INCLUDE_DIRS assimp/scene.h
      HINTS ${ASSIMP_INCLUDE_DIRS}
      PATHS "${CMAKE_INSTALL_PREFIX}/include")
  if (NOT TARGET assimp::assimp)
    add_library(assimp::assimp INTERFACE IMPORTED)
    set_target_properties(assimp::assimp PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${PC_ASSIMP_INCLUDE_DIRS}")
    set_target_properties(assimp::assimp PROPERTIES INTERFACE_LINK_LIBRARIES "${ASSIMP_LIBRARIES}")
  endif()
endif()
link_directories(${ASSIMP_LIBRARY_DIRS})
link_directories(${ASSIMP_LIBDIR})

# These targets are necessary for 16.04 builds. Remove when Kinetic support is dropped
if(NOT TARGET octomap)
  add_library(octomap INTERFACE IMPORTED)
  set_target_properties(octomap PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OCTOMAP_INCLUDE_DIRS}")
  set_target_properties(octomap PROPERTIES INTERFACE_LINK_LIBRARIES "${OCTOMAP_LIBRARIES}")
endif()
if(NOT TARGET octomath)
  add_library(octomath INTERFACE IMPORTED)
  set_target_properties(octomath PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OCTOMAP_INCLUDE_DIRS}")
  set_target_properties(octomath PROPERTIES INTERFACE_LINK_LIBRARIES "${OCTOMAP_LIBRARIES}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake")
