@PACKAGE_INIT@

set(@PROJECT_NAME@_FOUND ON)
set_and_check(@PROJECT_NAME@_INCLUDE_DIRS "${PACKAGE_PREFIX_DIR}/include")
set_and_check(@PROJECT_NAME@_LIBRARY_DIRS "${PACKAGE_PREFIX_DIR}/lib")

include(CMakeFindDependencyMacro)
if(${CMAKE_VERSION} VERSION_LESS "3.10.0")
    find_package(Boost COMPONENTS system thread program_options)
else()
    find_dependency(Boost COMPONENTS system thread program_options)
endif()
find_dependency(Eigen3)
find_dependency(octomap)
find_dependency(console_bridge)
find_dependency(tesseract_common)
find_dependency(tesseract_geometry)
find_dependency(tesseract_support)
find_dependency(fcl)
find_dependency(ccd)
find_dependency(Bullet3)

if (NOT TARGET bullet::bullet3)
  add_library(bullet::bullet3 INTERFACE IMPORTED)
  set_target_properties(bullet::bullet3 PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${BULLET_INCLUDE_DIRS}")
  set_target_properties(bullet::bullet3 PROPERTIES INTERFACE_LINK_LIBRARIES "${BULLET_LIBRARIES}")
  set_target_properties(bullet::bullet3 PROPERTIES INTERFACE_COMPILE_DEFINITIONS "${BULLET_DEFINITIONS}")
endif()
link_directories(${BULLET_LIBRARY_DIRS})

# These targets are necessary for 16.04 builds. Remove when Kinetic support is dropped
if(NOT TARGET octomap)
  add_library(octomap INTERFACE IMPORTED)
  set_target_properties(octomap PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OCTOMAP_INCLUDE_DIRS}")
  set_target_properties(octomap PROPERTIES INTERFACE_LINK_LIBRARIES "${OCTOMAP_LIBRARIES}")
endif()
if(NOT TARGET octomath)
  add_library(octomath INTERFACE IMPORTED)
  set_target_properties(octomath PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OCTOMAP_INCLUDE_DIRS}")
  set_target_properties(octomath PROPERTIES INTERFACE_LINK_LIBRARIES "${OCTOMAP_LIBRARIES}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake")
